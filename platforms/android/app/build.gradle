def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary

if (buildAsApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


/**
 * Read the Android ABI from user input.
 * supported ANDROID_ABIs are 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
 * @return return
 */
def List getAndroidABI() {
    String property = project.findProperty('ANDROID_ABI')

    List<String> AbiFilters = new ArrayList<String>()

    if (property == null) {
        AbiFilters.add("armeabi-v7a");
        AbiFilters.add("arm64-v8a");
        AbiFilters.add("x86");
        AbiFilters.add("x86_64");
    } else if (property.equalsIgnoreCase('armeabi-v7a')) {
        AbiFilters.add(property);
    } else if (property.equalsIgnoreCase('arm64-v8a')) {
        AbiFilters.add(property);
    } else if (property.equalsIgnoreCase('x86')) {
        AbiFilters.add(property);
    } else if (property.equalsIgnoreCase('x86_64')) {
        AbiFilters.add(property);
    } else {
        throw new Exception('ERROR: Invalid ANDROID_ABI entered: ' + property)
    }
    return AbiFilters
}

def checkIfBuildDirsExists(List<String> abis) {

    for (abi in abis) {
        String path = "../../../../build-" + abi;
        File file = project.file(project.getLayout().getBuildDirectory().file(path));
        if (!file.exists()) {
            throw new Exception('ERROR: build folder for abi \'' + abi + '\' doesn\'t  exist: ' + file);
        }

    }
}


android {
    compileSdk 34
    ndkVersion "26.2.11394342"
    defaultConfig {
        if (buildAsApplication) {
            applicationId "com.github.oopetris"
        }
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 3
        versionName "1.0.3"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
    }
    namespace 'com.github.oopetris'
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
                .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
    }

    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }
    }

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "com.github.oopetris.aar";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }
    }

    checkIfBuildDirsExists(getAndroidABI())

    splits {
        // Configures multiple APKs based on ABI.
        abi {
            // Enables building multiple APKs per ABI.
            enable true
            // By default all ABIs are included, so use reset() and include to specify that you only
            // want APKs for the ones with a ndk variant (4, android did in the past support more: https://developer.android.com/ndk/guides/abis.html#sa)
            // Resets the list of ABIs for Gradle to create APKs for to none.
            reset()
            // Specifies a list of ABIs for Gradle to create APKs for.
            include(*getAndroidABI())
            // Specifies that you don't want to also generate a universal APK that includes all ABIs.
            universalApk false
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
